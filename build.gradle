plugins {
    id 'java'
    id 'idea'
    id 'eclipse'
    id 'maven-publish'
    id 'io.spring.dependency-management' version '1.0.6.RELEASE'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.jfrog.artifactory' version '4.8.1'
}

repositories {
    jcenter()
}

// Dependency Management for Subprojects
dependencyManagement {
    dependencies {
        // Logging
        dependency group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
        dependency group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

        // Apache Commons
        dependency group: 'org.apache.commons', name: 'commons-lang3', version: '3.8.1'

        // Testing
        dependency group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.3.1'
        dependency group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.3.1'

        // Lombok
        dependency group: 'org.projectlombok', name: 'lombok', version: '1.18.2'
    }
}

dependencies {
    // Annotation processors
    compileOnly group: 'org.projectlombok', name: 'lombok'
    annotationProcessor group: 'org.projectlombok', name: 'lombok'
    testCompile group: 'org.projectlombok', name: 'lombok'
    testAnnotationProcessor group: 'org.projectlombok', name: 'lombok'

    // Apache Commons
    compile group: 'org.apache.commons', name: 'commons-lang3'

    // Logging
    compile group: 'org.slf4j', name: 'slf4j-api'
    testCompile group: 'ch.qos.logback', name: 'logback-classic'

    // Test
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
}

// Artifact Info
project.ext {
    groupId = 'com.github.philippheuer.common'
    artifactNamespace = 'PhilippHeuer'
    artifactName = 'common-exceptions'
    artifactVersion = String.valueOf(System.getenv("CI_COMMIT_REF_NAME")).replace("v", "")
    artifactDescription = 'Common - Exceptions'
    websiteUrl = 'https://github.com/PhilippHeuer/java-common-exceptions'
    issueTrackerUrl = 'https://github.com/PhilippHeuer/java-common-exceptions/issues'
    vcsUrl = 'https://github.com/PhilippHeuer/java-common-exceptions.git'
}

// Deployment Script
apply from: 'deployment.gradle'
